container Geography: using = "Units"
, Descr = "Definities van de verschillende grid domains die kunnen worden gebruikt in het model, RegioIndelingen, en afstandsmatrices."
{
	unit<DPoint> rdc_meter_base   : DialogData = "ngr_layer", SpatialReference = "EPSG:28992", Descr = "Geografisch domain in RD-coordinaten, gedefinieerd in meters.";
	unit<DPoint> rdc_meter        := range(rdc_meter_base, point_xy(x_min, y_min,rdc_meter_base), point_xy(x_max, y_max,rdc_meter_base)), Descr = "Geografisch domain in RD-coordinaten, afgebakend tot Nederland.";
	unit<IPoint> rdc_mm           := gridset(rdc_meter,  point_yx(0.001, 0.001), point_yx(0.0, 0.0), ipoint), Descr = "Geografisch domain in RD-coordinaten, afgebakend op het studiegebied, in millimeters (en dus integers)";

	unit<UInt32> Gridsizes : NrOfRows = 4
	, Descr = "Verschillende gridsizes die gebruikt kunnen worden in het model, in meters."
	{
		attribute<String>  size_str : ['5', '10', '25', '100'];
		attribute<meter>   size     := size_str[meter];
		attribute<String>  name    := 'size_'+string(size)+'m';
	}
	
	unit<IPoint> rdc_5m    := perGridsize/size_5m/domain;
	unit<IPoint> rdc_10m   := perGridsize/size_10m/domain;
	unit<IPoint> rdc_25m   := perGridsize/size_25m/domain;
	unit<IPoint> rdc_100m  := perGridsize/size_100m/domain;
	
	parameter<Float64> x_min     := 0d;
	parameter<Float64> x_max     := 280000d;
	parameter<Float64> y_min     := 300000d;
	parameter<Float64> y_max     := 625000d;
	
	container perGridsize := for_each_ne(Gridsizes/name, 'Create_GridDomain_T('+string(Gridsizes/size)+'[float64])'), Descr = "Bevat de verschillende grid domains die gebruikt kunnen worden in het model, op basis van de verschillende gridsizes.";
	
	unit<UInt8> Matrix : NrOfRows = 26
	, Descr = "Bevat de verschillende afstandsmatrices die gebruikt kunnen worden in het model."
	{
		attribute<meter>   size : [ 0, 17, 25, 50, 100, 150, 200, 300, 400, 500, 600, 625, 700, 1000, 1250, 1500, 2000, 2250, 2500, 3250, 5000, 5250, 10000, 10250, 20000, 20250 ];
		attribute<String>  name := 'pot'+string(size)+'m';
	}
	
	container Distmatrices_25m  := for_each_nex(Matrix/name, 'Distmatrices/Impl_25m/'+Matrix/name+'/PotRange', spoint), Descr = "Bevat de verschillende afstandsmatrices die gebruikt kunnen worden in het model, op basis van een grid van 25m.";
	container Distmatrices_100m := for_each_nex(Matrix/name, 'Distmatrices/Impl_100m/'+Matrix/name+'/PotRange', spoint), Descr = "Bevat de verschillende afstandsmatrices die gebruikt kunnen worden in het model, op basis van een grid van 100m.";

	#include<Distmatrices.dms>
	#include<Create_GridDomain_T.dms>
	#include<ngr_layer.dms>
}